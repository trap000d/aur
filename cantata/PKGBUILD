# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: goetzc
# Contributor: Kim Scarborough <sluggo@unknown.nu>

pkgname=cantata-qt6
pkgver=3.3.1
pkgrel=2
pkgdesc="Qt6 graphical client for Music Player Daemon (MPD), nullobsi fork"
arch=(x86_64 i686 aarch64 armv7h)
url="https://github.com/nullobsi/cantata"
license=(GPL3)
depends=(qt6-base
         qt6-multimedia
         qt6-svg
         libcdio-paranoia
         libmtp
         libmusicbrainz5
         libcddb
         taglib
         libebur128
         media-player-info
         mpg123
         #taglib-extras
         udisks2
         ffmpeg

        avahi gcc-libs zlib glibc
         )
makedepends=(git cmake qt6-tools)
optdepends=('perl-uri: Dynamic playlist'
            'mpd: Playback')
provides=(cantata)
conflicts=(cantata)
source=("cantata-nullobsi::git+https://github.com/nullobsi/cantata.git#tag=v${pkgver}"
        "001-fontawesome-fix-glyphs.patch"
        "002-disable-local-folders.patch")
sha256sums=('c282fd13610e6521696a9caaa5772952b16a9ff2a64192fdc379d9074ebdb96c'
            'eb5f06104fe521ffb97c3cf0c47ae117066d3a025bee302ba54fefff9799dc27'
            '06277601d7068e6ba4f8b8909537234f7f04b391a99bc518401d000ab5be946b')
prepare() {
  cd "cantata-nullobsi"
  patch -Np1 -R -i ../001-fontawesome-fix-glyphs.patch  
  patch -Np1 -i ../002-disable-local-folders.patch
}

build() {
  cmake -B build -S "cantata-nullobsi" -Wno-dev \
    -DQT_DIR=/usr/lib/cmake/Qt6 \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/bin

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
