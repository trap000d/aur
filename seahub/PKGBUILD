# Maintainer: Joffrey <j-off@live.fr>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seahub
pkgver=13.0.18
pkgrel=1
pkgdesc='The web frontend for seafile server'
arch=('any')
url='https://github.com/haiwen/seahub'
license=('Apache-2.0')
depends=(
    "seafile-server"
    'python-django'
    'python-django-statici18n'
    'python-django-webpack-loader'
    'python-django-picklefield'
    'python-django-formtools'
    'python-django-simple-captcha'
    'python-django-saml2'
    'python-django-rest-framework'
    'python-dateutil'
    'python-pyjwt'
    'python-pycryptodome'
    'python-requests'
    'python-requests-oauthlib'
    'gunicorn'
    'python-cairosvg'
    'python-mysqlclient'
    'python-qrcode'
    'python-pillow'
    'python-chardet'
    'python-cffi'
    'python-openpyxl'
    'python-markdown'
    'python-bleach'
    'python-ldap'
    'pypinyin'
    'python-dnspython'
    'python-pillow-heif'
)
optdepends=(
    'python-pymysql: Installation script'
    'python-wsgidav-seafile: Webdav support'
    'python-pylibmc: Memcached support'
    'ffmpeg: For video thumbnails'
)
#django_version=5.2.8
source=(
    "$pkgname-$pkgver-server.tar.gz::$url/archive/v$pkgver-server.tar.gz"
    #"python-django-$django_version.tar.gz::https://github.com/django/django/archive/$django_version.tar.gz"
    'seahub@.service'
    'nginx.example.conf'
    'fix_configuration_files.diff'
    'fix_django_memcached.diff')
sha256sums=('790358c52cb44eb30ac28fec5b57badae7ba40aa93232f1b3137119c73258e3e'
            '67bb375871ce908b48bef53277284c9d8f80ee2e733efc89cb66d987647195e4'
            '461591ba500d012523d6fdecbcc230461f6fd8d708b92eefdedc8b93b1542171'
            '58b7c67a69c55a949fed83ffe2c54b0fff868a04eb0df80b22c9ae8a0911e87b'
            '08ade0e034ebfd22eb8bc497ac3cfce26d1cf7faa2fbfb2891aeb3a0b48991d6')
options=('!strip')

prepare() {
    cd "$srcdir/$pkgname-$pkgver-server"

    patch -p1 -i "$srcdir/fix_configuration_files.diff"
    patch -p1 -i "$srcdir/fix_django_memcached.diff"

    # Remove useless files and directories
    rm -rf \
        ./{CONTRIBUTORS,HACKING,Makefile} \
        ./{*test*,*dev*,*sh*,README*,pylintrc*,LICENSE*} \
        ./scripts/{build,pro.py,*.{md,conf}}
    find . -type f \( -name '*.pyc' -o -name '.git*' \) -delete

    # Fix paths to Gunicorn
    sed -e 's|gunicorn_exe=.*|gunicorn_exe=/usr/bin/gunicorn|g' \
        -e 's|thirdpart/bin/gunicorn|$gunicorn_exe|g' \
        -i ./scripts/seahub.sh

    sed -i -E "/SEAFILE_VERSION/s/[0-9.]+/$pkgver/" ./seahub/settings.py
}

build() {
    cd "$srcdir/$pkgname-$pkgver-server"

    for locale in ./locale/*/LC_MESSAGES/*.po; do
        echo "$locale"
        msgfmt -vo "${locale%.po}.mo" "$locale"
    done
}

package() {
    cd "$srcdir/seahub-$pkgver-server/"

    install -dm755 "$pkgdir/usr/share/seafile-server/seahub"
    cp -rp ./* "$pkgdir/usr/share/seafile-server/seahub"
    mv "$pkgdir/usr/share/seafile-server/seahub/scripts/"* \
        "$pkgdir/usr/share/seafile-server"

    ##pushd "$srcdir/django-$django_version"
    ##cp -vr django -t "$pkgdir/usr/share/seafile-server/$pkgname/thirdpart"
    #python -m build --wheel --no-isolation
    #python -m wheel unpack --dest="$pkgdir/usr/share/seafile-server/$pkgname/thirdpart" dist/*.whl
    #python -m install
    #    --root="$pkgdir/" \
    #    --install-lib="usr/share/seafile-server/$pkgname/thirdpart" \
    #    --optimize=0
    ##popd
    rm -rf "$pkgdir"/usr/{bin,share/seafile-server/"$pkgname"/thirdpart/*.egg-info}

    python -m compileall -f -j 0 -o 1 \
        -s "$pkgdir" -p / "$pkgdir/usr/share/seafile-server/seahub"

    install -Dm644 \
        "$srcdir/seahub@.service" \
        "$pkgdir/usr/lib/systemd/system/seahub@.service"
    install -Dm644 \
        "$srcdir/nginx.example.conf" \
        "$pkgdir/etc/webapps/$pkgname/nginx.conf"
}
